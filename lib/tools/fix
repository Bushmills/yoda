provides: fix review
( string: $1 -- )  ( -- f )
: used       "editor" env$ $= ;

\ "error" places filename, error line and error column in lasterror[0], [1] and [2].
\ error line and columns can be passed to editor for moving cursor to error location.
\ but as editor invocation lines differs, here's a selection of two handfuls:

"less"       used lest >fixargs='+${lasterror[1]}'
"most"       used lest >fixargs='+${lasterror[1]}'
"nano"       used lest >fixargs='+${lasterror[1]},${lasterror[2]}'
"mcedit"     used lest >fixargs='+${lasterror[1]}'
"jupp"       used lest >fixargs='+${lasterror[1]}'
"joe"        used lest >fixargs='+${lasterror[1]}'
"vi"         used lest >fixargs='+${lasterror[1]}'
"emacs"      used lest >fixargs='+${lasterror[1]}:${lasterror[2]}'
"featherpad" used lest >fixargs='+${lasterror[1]},$((${lasterror[2]}-1))'
"mousepad"   used lest >fixargs='-l ${lasterror[1]} -c $((${lasterror[2]}-1))'
"efte"       used lest >fixargs='-l${lasterror[1]},${lasterror[2]}'
"gedit"      used lest >fixargs='+${lasterror[1]}:${lasterror[2]}'
trash used   \ used is a too common word to waste it for this: discard header

\ in case error occured while not loading from file, number in lasterror[1] (the line number)
\ will be negative. Therefore will fix refuse to open a file when lasterror[1] is negative.

: fix
    "lasterror[1]" env
    0< if "noerror" shell$ exit then
    >eval editor "$fixargs" "$lasterror"
;

\ much less not pretty now, and still useful
: review
   word$ where$                        \ parse name of word to review, retrieve source location
   "lasterror[1]" >env                 \ store line number in parameterrecord for fix
   1 "lasterror[2]" >env               \ store column number (always 1) in parameter record for fix
   filename$                           \ convert file handle to file name
   "lasterror[0]" >env$                \ store file name in parameter record fix fix
   fix ;                               \ call fix, pretending that there's an error at source location
