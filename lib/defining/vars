( linked variables can be displayed in a list with contents displayed )
provides: var .vars
needs: link

variable vars               \ link anchor as base of linked list through variables

: var ( create: -- )  ( run: -- a )
    vars link               \ add linked list item
    here cell+ constant     \ skip cell to store xt for name
    last @ ,                \ add xt
    0 ,  ;                  \ variable data

\ variables are created like this:
\ var foo
\ var bar

\ output name and value of a single variable
: .var ( a -- )
    skim name$ dup$ type$
    @ #16 count$ - .r
    cr  ;

\ produce a table showing names and values of variables
: .vars  ( -- )
    vars                    \ begin at link anchor
    begin @                 \ move to next variable
    ?dup while              \ unless there isn't any next variable
       dup cell+ .var       \ output name and value of variable
    repeat ;
