provides: statusline +l -l
needs: uppercase movecursor bold fg deol .s columns

create statusline   true ,
2 constant spacing               transient

: +l   statusline on ;
: -l   statusline off
       0 0 movecursor deol         \ empty top line
       restorecursor ;


: show0           ( $1 -- )
        type$
        spacing spaces ;
transient


: show1           ( x -- ) ( $1 -- )
        base @ >r
        decimal
        type$ ': emit 0 u.r
        spacing spaces
        r> base ! ;
transient


: show$           ( $1 $2 -- )
        swap$ type$
        ': emit show0 ;
transient


: status          ( c -- )  ( $1 -- $2 )
        if uppercase
        then append$ ;
transient

: flags  ( -- $1 )
        "flags" ""
        'f  forwardrefs @     status
        'i  instantresolve @  status
        's  shell @           status  ;
transient


: plenty  ( columns width prio -- columns flag )
   spacing * + over < ;
transient


\ depth of call stack, corrected for base level.
\ every error or break increase depth. This value
\ is displayed as indicator for when a yoda restart
\ may be desirable.
: calls  ( -- u )  [ "${#FUNCNAME[@]}-6" ] push$ ;
: xts    ( -- u )  [ "xt-firstxt" ] push$ ;



\ 120 columns for full status line
\ NOTE: define a version of .s which fits output into available space, truncated if necessary
: .status  ( -- )
   columns
      bold yellow fg  deol
\     +------ space required
\     |  +--- priority ( 1 is highest )
\     |  |
     132 11 plenty if "yoda version " type$ "v" env$        show0  then
     115 10 plenty if                 "bits"    bits        show1  then
       9  1 plenty if                           flags       show$  then
      30  4 plenty if                 "call"    calls       show1  then
      64  8 plenty if                 "xts"     xts         show1  then
      39  5 plenty if                 "files"   #files      show1  then
      16  2 plenty if                 "unres"   #unresolved show1  then
      23  3 plenty if                 "base"    base @      show1  then
      47  6 plenty if                 "here"    here        show1  then
      56  7 plenty if                 "rdepth"  rdepth      show1  then
     108  9 plenty if                 "stack:"  type$   >r .s r>   then
   drop
;

\ increasing window size vertical creates screen relicts if not done gently
: redraw  ( -- )
        statusline @ lest
        0 0 movecursor
        .status deol cr deol
        restorecursor  ;
transient

' redraw "SIGWINCH" trap
