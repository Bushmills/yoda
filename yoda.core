># ---- primitives ---------------------------------------

>set-current "$forthwid"

># ( -- )  ( $1 -- )
: primitive
      code
      postpone code,
      ;code  >inline
 ;

"inline"                                  primitive inline
"immediate"                               primitive immediate
"parse ')'"                               primitive (                ( -- ) ( s: delim )           immediate
"line=''"                                 primitive \                ( -- ) ( s: line )            immediate

"interpreted"                             primitive interpreted      ( -- )
"compiled"                                primitive compiled         ( -- )
"protect"                                 primitive protect          ( -- )
"wordsplus"                               primitive words+           ( -- )
"set-current ${s[sp--]}"                  primitive set-current      ( wid -- )

"m[base]=10"                              primitive decimal          ( -- )
"m[base]=16"                              primitive hex              ( -- )
"m[base]=2"                               primitive binary           ( -- )

"((sp-=s[sp]+1))"                         primitive ndrop            ( [x items] u -- [x-u items] )
"((sp--))"                                primitive drop             ( x -- )
"((rp--))"                                primitive rdrop            ( -- ) ( r: x -- )
"((sp-=2))"                               primitive 2drop            ( x1 x2 -- )

"code 'break'"                            primitive leave            ( -- )                        compiled
"warm"                                    primitive warm             ( ??? - ) ( r: ??? -- )
"exit 0"                                  primitive bye              ( -- )
"s=() sp=0"                               primitive empty            ( ??? -- )
"r=() rp=0"                               primitive rempty           ( -- ) ( r: ??? -- )
"((s[sp]=(s[sp]+1)&true))"                primitive 1+               ( x1 -- x2 )
"((s[sp]=(s[sp]-1)&true))"                primitive 1-               ( x1 -- x2 )
"((s[sp]=(s[sp]<<1)&true))"               primitive 2*               ( x1 -- x2 )
"((s[sp]^=maxuint))"                      primitive invert           ( x1 -- x2 )
"((s[sp]=(-s[sp]&true)))"                 primitive negate           ( n1 -- n2 )

"((m[dp++]=s[sp--]&255))"                 primitive c,               ( c -- )
"((s[sp]=m[s[sp]]))"                      primitive @                ( a -- x )

\ compiles exit into word under
\ construction due to inline, which is
\ therefore essential. primitives have
\ inline flag set by default.
"return"                                  primitive exit             ( -- )

"push ${s[sp--]}"                         primitive literal,         ( -- ) ( compile time: x -- ) ( run time: -- x )
"call ${s[sp--]}"                         primitive compile,         ( -- ) ( compile time: x -- ) ( run time: -- x )

' 1+ alias char+  inline

\ ----- manage wordlists ---------------------------------

code get-context  ( -- wid )              "order[0]"                 push,    ;code                inline
code set-context  ( wid -- )              "order[0]"                 pop,     ;code                inline

>set-current      "$onlywid"
"words"                                   primitive words            ( -- )                        inline
"order=($onlywid)"                        primitive onlyonly         ( -- )                        inline transient
: definitions     ( -- )                  get-context set-current             ;                    inline
: only            ( -- )                  onlyonly also definitions           ;                    inline
code forthwid     ( -- wid )              "forthwid"     push,                ;code                inline transient
: forth           ( -- )                  forthwid set-context                ;                    inline
only forth definitions

\ ----- stack --------------------------------------------

\ code sp!       ( ??? u -- [u items] )               "sp"                          pop,  ;code    inline
code depth     ( ?? -- ?? u )                         "sp"                          push, ;code    inline
code 2over     ( x1 x2 x3 x4 -- x1 x2 x3 x4 x1 x2 )   "s[sp-3]"         dup$ push,  push, ;code    inline
code dup       ( x -- x x )                           "s[sp]"                       push, ;code    inline
code over      ( x1 x2 -- x1 x2 x1 )                  "s[sp-1]"                     push, ;code    inline
code pluck     ( x1 x2 x3 -- x1 x2 x3 x1 )            "s[sp-2]"                     push, ;code    inline
code pick      ( ... x2 x1 x0 u -- ... x2 x1 x0 xu )  "s[sp-s[sp--]-1]"             push, ;code    inline
code r@        ( -- x )  ( r: x -- x )                "r[rp]"                       push, ;code    inline
code >r        ( x -- ) ( r: -- x )                   "r[++rp]"                     pop,  ;code    inline
code r>        ( -- x ) ( r: x -- )                   "r[rp--]"                     push, ;code    inline
code rdepth    ( -- u )                               "rp"                          push, ;code    inline
code depth$    ( -- u )                               "${#ss[@]}"                   push, ;code    inline
code 2r>       ( -- x1 x2 )  ( r: x1 x2 -- )          "r[rp-1]"                     push,
                                                      "r[rp--]"                     push,
                                                      "((rp--))"                    code, ;code    inline
code 2>r       ( x1 x2 -- )  ( return: -- x1 x2 )     "((r[++rp]=s[sp-1]))"         code,
                                                      "r[++rp]"                     pop,
                                                      "((sp--))"                    code, ;code    inline
: nip          ( x1 x2 -- x2 )                        [ "s[sp-1]=s[sp]" ]  code,    drop  ;        inline
: ?dup         ( x -- 0 | x x )                       [ "((s[sp])) &&" ]   code,    dup   ;        inline
: 2dup         ( x1 x2 -- x1 x2 x1 x2 )               over over                           ;        inline

\ ----- flow control -------------------------------------

code i         ( -- x )                               "i"                           push, ;code    inline
code unloop    ( -- x )                               "((i=r[rp--]))"               code,
                                                      "((ibar=r[rp--]))"            code, ;code    inline
' r@ alias j                                                                                       inline
code ?leave    ( flag -- )                            "code '((s[sp--]))&&'"        code,
                                                      "code 'break'"                code, ;code    compiled

\ ----- arithmetics --------------------------------------

code dnegate   ( d1 -- d2 )                           "((s[sp-1]=(~(s[sp-1])+1)&maxuint))" code,
                                                      "((s[sp]^=maxuint))"         code,
                                                      "((s[sp-1]))||((s[sp]++))"   code,  ;code

' noop alias cells                                                                                 immediate
' lshift alias <<                                                                                  inline
' rshift alias >>                                                                                  inline

: ?negate      ( n flag -- n | -n )                   [ "((s[sp--]))&&" ]   code, negate  ;        inline
: abs          ( n -- u )                             [ "((s[sp]&msb))&&" ] code, negate  ;        inline
: /mod         ( n1 n2 -- n3 n4 )                     1 -rot  */mod                       ;                 \ divisions are truncated, symmetric
: */           ( n1 n2 n3 -- n4 )                     */mod nip                           ;        inline
: /            ( n1 n2 -- n3 )                        /mod nip                            ;        inline
: *            ( n1 n2 -- n3 )                        1 */                                ;        inline
: mod          ( n1 n2 -- n3 )                        /mod drop                           ;        inline
: %            ( n1 n2 -- n3 )                        #50 */ dup 0< 1 or + 2/             ;                 \ percentage rounded, not truncated
: min          ( n1 n2 -- n1|n2 )                     2dup >  if nip exit then drop       ;
: max          ( n1 n2 -- n1|n2 )                     2dup <  if nip exit then drop       ;
: umin         ( u1 u2 -- u1|u2 )                     2dup u> if nip exit then drop       ;
: umax         ( u1 u2 -- u1|u2 )                     2dup u< if nip exit then drop       ;
\ : umax       ( u1 u2 -- u1|u2 )                     2dup u< if [ "s2=s1" ] atom, then drop ;              \ alternative. shorter, but not faster
: cell+        ( x1 -- x2 )                           1+                                  ;        inline
: s>d          ( n -- d )                             dup 0<                              ;        inline
: within       ( x low high -- flag )                 over - >r - r> u<                   ;
: um/mod       ( d u1 -- u2 u3 )                      ud/mod drop                         ;        inline   \ d / u1 -> quotient u3 remainder u2
: dabs         ( d1 -- d2 )                           dup 0< lest dnegate                 ;


\ ----- memory -------------------------------------------


code ,         ( x -- )                               "m[dp++]"               pop,        ;code    inline
code count     ( a -- a+1 c )                         "m[s[sp]++]&255"        push,       ;code    inline
code skim      ( a -- a+cell x )                      "m[s[sp]++]"            push,       ;code    inline
code here$     ( -- u )                               "sdp"                   push,       ;code    inline
code here      ( -- a )                               "dp"                    push,       ;code    inline

: erase        ( a u -- )                             0 fill ;                                     inline      \ 0 -> m[a++],  u times
: <-           ( a x -- )                             [ "m[s[sp]]" ]          pop, drop   ;        inline      \ same as:  swap !
: count$       ( -- u ) ( $1 -- )                     [ "${#ss[-1]}" ]        push, drop$ ;        inline
: env          ( -- x ) ( string:  $1 -- )            [ "${!ss[-1]}+0&true" ] push, drop$ ;        inline

\ ----- strings ------------------------------------------

: pad          ( -- a )                               here #256 +                         ;        inline

\ ----- i/o ----------------------------------------------

defer query$                                          ' simplequery$ is query$

: type         ( c-addr u -- )                        pack$ type$                         ;        inline
: ltype$       ( u -- ) ( $1 -- )                     dup$ type$ count$ - spaces          ;
: rtype$       ( u -- ) ( $1 -- )                     dup$ count$ - spaces type$          ;
: word$        ( -- ) ( -- $1 )                       bl parse$                           ;        inline

\ ----- pictured number conversion -----------------------

' prepend$ alias hold                                                                              inline
: holds$       ( -- ) ( $1 $2 -- $3 )                 swap$ join$                         ;        inline
: holds        ( c-addr u -- )                        pack$ holds$                        ;        inline
: sign         ( f -- ) ( $1 -- $2 )                  0< lest '-' hold                    ;
: #            ( d1 -- d2 )  ( string: $1 -- $2 )     base @  ud/mod
                                                      rot '0 +
                                                      dup '9 u> #39 and +
                                                      hold                                ;
' 2drop alias #>$                                                                                  inline
: <#           ( -- $1 )                              ""                                  ;        inline
: #s           ( d1 -- d2 )                           begin # 2dup or 0= until            ;
: #>type       ( d -- ) ( $1 -- )                     #>$ type$                           ;        inline
: #>           ( d -- )                               #>$ here here unpack$               ;
: udconvert    ( ud -- ) ( -- $1 )                    <# #s #>$                           ;
: dconvert     ( d -- ) ( -- $1 )                     tuck dabs udconvert sign            ;
: d.           ( d -- )                               dconvert type$ space                ;
: ud.          ( ud -- )                              udconvert type$ space               ;
: d.r          ( d u -- )                             >r  dconvert r> rtype$              ;
: ud.r         ( ud u -- )                            >r udconvert r> rtype$              ;

' drop alias x>$                                                                                   inline transient
' <# alias <x                                                                                      inline transient
: xs           ( d -- d )                             begin x dup 0= until                ;        transient
: x>type       ( x -- ) ( $1 -- )                     x>$ type$                           ;        inline transient
: x>           ( x -- a n )( $1 -- )                  x>$ here here unpack$               ;        transient
trash x

: uconvert     ( u -- ) ( -- $1 )                     <x xs x>$                           ;
: convert      ( n -- ) ( -- $1 )                     dup abs uconvert sign               ;
: .            ( n -- )                               convert type$ space                 ;
: u.           ( u -- )                               uconvert type$ space                ;
: .r           ( n u -- )                             >r  convert r> rtype$               ;
: u.r          ( u1 u2 -- )                           >r uconvert r> rtype$               ;

\ ----- dictionary and compiling -------------------------
code get-current  ( -- wid )                          "currentwid"               push,    ;code    inline
code #unresolved  ( -- u )                            "${#unresolved[@]}"        push,    ;code    inline
code compiling ( -- flag )                            "compiling"                push,    ;code    inline
code state     ( -- a )                               "m[statevar]=$compiling"   code,
                                                      "statevar"                 push,    ;code    inline
trash statevar

' have$ alias xt                                                                                   inline
' satisfy alias needs:                                                                             inline

\ produces execution token and removes header of last word:  : foo ... ; noname
: noname       ( -- xt )                              last$ dup$ xt   protect trash$      ;
: me           ( compile: -- )   ( run: -- x )        [ "xt" ] push,          literal,    ;        compiled
: needed       ( -- f ) ( stream: word )              word$ needed$                       ;        inline
: literal      ( x -- )                               literal,                            ;        compiled
: have         ( -- 0|xt ) ( stream: word )           word$ have$                         ;        inline      \ word exists?
: lack         ( -- flag ) ( stream: word )           have  0=                            ;        inline      \ word missing?
: [']          ( -- )   ( stream: word )              ' literal,                          ;        compiled

\ ----- unsorted -----------------------------------------

code random    ( -- u )                               "SRANDOM&true"             push,    ;code    inline      \  32 bits random number
code realtime  ( -- u )                               "${EPOCHREALTIME//[,.]/}&true" push, ;code   inline      \ a 51 bit microseconds epoch

: quit         ( ??? -- )                             warm
                                                      begin
                                                         query$ evaluate$
                                                         resolve
                                                         prompt
                                                      again                               ;

"cold" primitive cold
' cold alias abort
: (abort")     ( ??? flag -- ??? | empty )            if type$ cr abort then drop$        ;
: abort"       ( -- ) ( s: string deliited by " )     '" parse$
                                                      postpone literal$
                                                      ['] (abort") compile,               ;        compiled
: ?abort       ( f -- )                               lest abort                          ;

\ ----- files and loading --------------------------------

' origin alias thisfile                                                                                        \ better symmetry with thisfile$

\ check whether current source had already been
\ compiled from before. most useful when used
\ interpretatively in a file, for testing whether
\ it will be or has been reloaded, such as with
\ "loaded lest done" returned u is the count
\ the file has been loaded.
code loaded    ( -- u )                               "loaded[filehandle]"       push,    ;code    inline

\ true: loading from file?,  false: interactive
\ (console). pipe or redirection is indicated as
\ loading from file.
code loading   ( -- flag )                            "linenr<0?false:true"      push,    ;code    inline
code #files    ( -- u )                               "${#files[@]}"             push,    ;code    inline

: once         ( -- )                                 loaded lest done                    ;                    \ prevent reloading file
: from         ( -- ) ( stream: filename )            word$ from$                         ;        inline
: thisfile$    ( -- )   ( -- $1 )                     thisfile filename$                  ;        inline      \ produce name of currently loaded file

\ ----- experiments --------------------------------------

done
omit
\ modify word name prior to header creation
\ background: let a word with "compiled" properties change header flags (issue 17)
: rather ( $1 -- )
       >body[0]="${ss[-1]}"
      drop$  ;
: foo  [ "bar" rather ]   me .name cr  ;
