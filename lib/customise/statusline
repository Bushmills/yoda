provides: statusline +l -l
needs: capitalise movecursor bold blue fg bg deol .s columns

create statusline   true ,
2 constant spacing

: +l   statusline on ;
: -l   statusline off
       0 0 movecursor deol         \ empty top line
       restorecursor ;

: show0           ( $1 -- )
        type$
        spacing spaces ;

: show1           ( x -- ) ( $1 -- )
        base @ >r
        decimal
        type$ ': emit 0 u.r
        spacing spaces
        r> base ! ;

: show$           ( $1 $2 -- )
        swap$ type$
        ': emit show0 ;

: status          ( c -- )  ( $1 -- $2 )
        if capitalise
        then append$ ;

: flags  ( -- $1 )
        "flags" ""
        'f  forwardrefs @     status
        'i  instantresolve @  status
        's  shell @           status  ;

: plenty  ( columns width prio -- columns flag )
   spacing * + over < ;

\ 120 columns for full status line
: .status  ( -- )
   columns
      bold yellow fg  ( blue bg )  deol
\     +------ space required
\     |  +--- priority ( 1 is highest )
\     |  |
      98 9 plenty if "yoda version " type$ "v" env$        show0  then
      57 7 plenty if                 "bits"    bits        show1  then
       9 1 plenty if                           flags       show$  then
      32 4 plenty if                 "files"   #files      show1  then
      17 2 plenty if                 "unres"   #unresolved show1  then
      24 3 plenty if                 "base"    base @      show1  then
      41 5 plenty if                 "here"    here        show1  then
      50 6 plenty if                 "rdepth"  rdepth      show1  then
      80 8 plenty if                 "stack:"  type$   >r .s r>   then
   drop
;

trash status
trash flags
trash plenty

\ increasing window size vertical creates screen relicts if not done gently
: redraw  ( -- )
        statusline @ lest
        0 0 movecursor
        deol .status cr deol
        restorecursor  ;

>trap ${header_code}_${m[last]} SIGWINCH
